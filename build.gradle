plugins {
    id 'jacoco'
    id 'java-library'
    id 'maven-publish'

    id 'com.diffplug.gradle.spotless' version '3.26.0'
}

repositories {
    mavenLocal()
    mavenCentral()
}

group = groupId
version = artifactVersion

sourceCompatibility = 11
targetCompatibility = 11

spotless {
    java {
        encoding 'UTF-8'
        removeUnusedImports()
        eclipse().configFile "${projectDir}/config/codestyle.xml"
    }
}

configurations {
    all {
        resolutionStrategy {
            cacheChangingModulesFor(1000, 'seconds') // check for updates every build
        }
    }
    micronautBom
    compileOnly.extendsFrom(micronautBom)
    implementation.extendsFrom(micronautBom)
    annotationProcessor.extendsFrom(micronautBom)
    testAnnotationProcessor.extendsFrom(micronautBom)
}

dependencies {
    micronautBom enforcedPlatform('io.micronaut:micronaut-bom:1.2.6')

    annotationProcessor 'io.micronaut.configuration:micronaut-openapi'
    annotationProcessor 'io.micronaut:micronaut-inject-java'

    compileOnly 'io.micronaut:micronaut-inject-java'
    compileOnly 'io.swagger.core.v3:swagger-annotations'

    implementation 'io.micronaut:micronaut-http'
    implementation 'io.micronaut:micronaut-security'

    testAnnotationProcessor 'io.micronaut:micronaut-inject-java'
    testCompileOnly 'io.micronaut:micronaut-inject-java'

    testCompile 'io.micronaut:micronaut-runtime'
    testCompile 'io.micronaut:micronaut-http-client'
    testCompile 'io.micronaut.test:micronaut-test-core'
    testCompile 'io.micronaut.test:micronaut-test-junit5'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation "org.junit.platform:junit-platform-launcher:$jupiterLauncher"
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }

    environment([
            "USE_INTEGRATION_TESTS"   : "$useIntegrationTests",
    ])
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username System.getenv("OSS_USER")
                password System.getenv("OSS_PASS")
            }
        }
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

check.dependsOn jacocoTestReport
jacocoTestReport {
    reports {
        xml.enabled true
        html.destination file("${buildDir}/jacocoHtml")
    }
}

if (project.hasProperty("signing.keyId")) {
    apply plugin: 'signing'
    signing {
        sign publishing.publications.mavenJava
    }
}